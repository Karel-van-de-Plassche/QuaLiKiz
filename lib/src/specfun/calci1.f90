SUBROUTINE CALCI1(ARG,RESULT,JINT)
  !--------------------------------------------------------------------
  !
  ! This packet computes modified Bessel functioons of the first kind
  !   and order one, I1(X) and EXP(-ABS(X))*I1(X), for real
  !   arguments X.  It contains two function type subprograms, BESI1
  !   and BESEI1, and one subroutine type subprogram, CALCI1.
  !   The calling statements for the primary entries are
  !
  !                   Y=BESI1(X)
  !   and
  !                   Y=BESEI1(X)
  !
  !   where the entry points correspond to the functions I1(X) and
  !   EXP(-ABS(X))*I1(X), respectively.  The routine CALCI1 is
  !   intended for internal packet use only, all computations within
  !   the packet being concentrated in this routine.  The function
  !   subprograms invoke CALCI1 with the statement
  !          CALL CALCI1(ARG,RESULT,JINT)
  !   where the parameter usage is as follows
  !
  !      Function                     Parameters for CALCI1
  !       Call              ARG                  RESULT          JINT
  !
  !     BESI1(ARG)    ABS(ARG) .LE. XMAX        I1(ARG)           1
  !     BESEI1(ARG)    any real ARG        EXP(-ABS(ARG))*I1(ARG) 2
  !
  !   The main computation evaluates slightly modified forms of
  !   minimax approximations generated by Blair and Edwards, Chalk
  !   River (Atomic Energy of Canada Limited) Report AE!L-4928,
  !   October, 1974.  This transportable program is patterned after
  !   the machine-dependent FUNPACK packet NATSI1, but cannot match
  !   that version for efficiency or accuracy.  This version uses
  !   rational functions that theoretically approximate I-SUB-1(X)
  !   to at least 18 significant decimal digits.  The accuracy
  !   achieved depends on the arithmetic system, the compiler, the
  !   intrinsic functions, and proper selection of the machine-
  !   dependent constants.
  !
  !*******************************************************************
  !*******************************************************************
  !
  ! Explanation of machine-dependent constants
  !
  !   beta   = Radix for the floating-point system
  !   maxexp = Smallest power of beta that overflows
  !   XSMALL = Positive argument such that 1.0 - X = 1.0 to
  !            machine precision for all ABS(X) .LE. XSMALL.
  !   XINF =   Largest positive machine number; approximately
  !            beta**maxexp
  !   XMAX =   Largest argument acceptable to BESI1;  Solution to
  !            equation: 
  !               EXP(X) * (1-3/(8*X)) / SQRT(2*PI*X) = beta**maxexp
  !
  !
  !     Approximate values for some important machines are:
  !
  !                          beta       maxexp       XSMALL
  !
  ! CRAY-1        (S.P.)       2         8191       3.55E-15
  ! Cyber 180/855
  !   under NOS   (S.P.)       2         1070       3.55E-15
  ! IEEE (IBM/XT,
  !   SUN, etc.)  (S.P.)       2          128       2.98E-8
  ! IEEE (IBM/XT,
  !   SUN, etc.)  (D.P.)       2         1024       5.55D-17
  ! IBM 3033      (D.P.)      16           63       6.95D-18
  ! VAX           (S.P.)       2          127       2.98E-8
  ! VAX D-Format  (D.P.)       2          127       6.95D-18
  ! VAX G-Format  (D.P.)       2         1023       5.55D-17
  !
  !
  !                               XINF          XMAX
  !
  ! CRAY-1        (S.P.)       5.45E+2465     5682.810
  ! Cyber 180/855
  !   under NOS   (S.P.)       1.26E+322       745.894
  ! IEEE (IBM/XT,
  !   SUN, etc.)  (S.P.)       3.40E+38         91.906
  ! IEEE (IBM/XT,
  !   SUN, etc.)  (D.P.)       1.79D+308       713.987
  ! IBM 3033      (D.P.)       7.23D+75        178.185
  ! VAX           (S.P.)       1.70D+38         91.209
  ! VAX D-Format  (D.P.)       1.70D+38         91.209
  ! VAX G-Format  (D.P.)       8.98D+307       713.293
  !
  !*******************************************************************
  !*******************************************************************
  !
  ! Error returns
  !
  !  The program returns the value XINF for ABS(ARG) .GT. XMAX.
  !
  !
  ! Intrinsic functions required are:
  !
  !     ABS, SQRT, EXP
  !
  !
  !  Authors: W. J. Cody and L. Stoltz
  !           Mathematics and Computer Science Division
  !           Argonne National Laboratory
  !           Argonne, IL  60439
  !
  !  Latest modification: May 31, 1989
  !  J.Citrin May 2014, transcribed to F90
  !
  !--------------------------------------------------------------------
  INTEGER J,JINT
  DOUBLE PRECISION :: A,ARG,B,EXP40,FORTY,HALF,ONE,ONE5,P,PBAR,PP,Q,QQ,REC15, &
       RESULT,SUMP,SUMQ,TWO25,X,XINF,XMAX,XSMALL,XX,ZERO
  DIMENSION P(15),PP(8),Q(5),QQ(6)
  !--------------------------------------------------------------------
  !  Mathematical constants
  !--------------------------------------------------------------------
  DATA ONE/1.0D0/,ONE5/15.0D0/,EXP40/2.353852668370199854D17/, &
       FORTY/40.0D0/,REC15/6.6666666666666666666D-2/, &
       TWO25/225.0D0/,HALF/0.5D0/,ZERO/0.0D0/
  !--------------------------------------------------------------------
  !  Machine-dependent constants
  !--------------------------------------------------------------------
  DATA XSMALL/5.55D-17/,XINF/1.79D308/,XMAX/713.987D0/
  !--------------------------------------------------------------------
  !  Coefficients for XSMALL .LE. ABS(ARG) .LT. 15.0
  !--------------------------------------------------------------------
  DATA P/-1.9705291802535139930D-19,-6.5245515583151902910D-16, &
       -1.1928788903603238754D-12,-1.4831904935994647675D-09, &
       -1.3466829827635152875D-06,-9.1746443287817501309D-04, &
       -4.7207090827310162436D-01,-1.8225946631657315931D+02, &
       -5.1894091982308017540D+04,-1.0588550724769347106D+07, &
       -1.4828267606612366099D+09,-1.3357437682275493024D+11, &
       -6.9876779648010090070D+12,-1.7732037840791591320D+14, &
       -1.4577180278143463643D+15/
  DATA Q/-4.0076864679904189921D+03, 7.4810580356655069138D+06, &
       -8.0059518998619764991D+09, 4.8544714258273622913D+12, &
       -1.3218168307321442305D+15/
  !--------------------------------------------------------------------
  !  Coefficients for 15.0 .LE. ABS(ARG)
  !--------------------------------------------------------------------
  DATA PP/-6.0437159056137600000D-02, 4.5748122901933459000D-01, &
       -4.2843766903304806403D-01, 9.7356000150886612134D-02, &
       -3.2457723974465568321D-03,-3.6395264712121795296D-04, &
       1.6258661867440836395D-05,-3.6347578404608223492D-07/
  DATA QQ/-3.8806586721556593450D+00, 3.2593714889036996297D+00, &
       -8.5017476463217924408D-01, 7.4212010813186530069D-02, &
       -2.2835624489492512649D-03, 3.7510433111922824643D-05/
  DATA PBAR/3.98437500D-01/
  !--------------------------------------------------------------------
  X = ABS(ARG)
  IF (X .LT. XSMALL) THEN
     !--------------------------------------------------------------------
     !  Return for ABS(ARG) .LT. XSMALL
     !--------------------------------------------------------------------
     RESULT = HALF * X
  ELSE IF (X .LT. ONE5) THEN
     !--------------------------------------------------------------------
     !  XSMALL .LE. ABS(ARG) .LT. 15.0
     !--------------------------------------------------------------------
     XX = X * X
     SUMP = P(1)
     DO 50 J = 2, 15
        SUMP = SUMP * XX + P(J)
50      CONTINUE
        XX = XX - TWO25
        SUMQ = ((((XX+Q(1))*XX+Q(2))*XX+Q(3))*XX+Q(4))*XX+Q(5)
        RESULT = (SUMP / SUMQ) * X
        IF (JINT .EQ. 2) RESULT = RESULT * EXP(-X)
     ELSE IF ((JINT .EQ. 1) .AND. (X .GT. XMAX)) THEN
        RESULT = XINF
     ELSE
        !--------------------------------------------------------------------
        !  15.0 .LE. ABS(ARG)
        !--------------------------------------------------------------------
        XX = ONE / X - REC15
        SUMP = ((((((PP(1)*XX+PP(2))*XX+PP(3))*XX+ &
             PP(4))*XX+PP(5))*XX+PP(6))*XX+PP(7))*XX+PP(8)
        SUMQ = (((((XX+QQ(1))*XX+QQ(2))*XX+QQ(3))*XX+ &
             QQ(4))*XX+QQ(5))*XX+QQ(6)
        RESULT = SUMP / SUMQ
        IF (JINT .NE. 1) THEN
           RESULT = (RESULT + PBAR) / SQRT(X)
        ELSE
           !--------------------------------------------------------------------
           !  Calculation reformulated to avoid premature overflow
           !--------------------------------------------------------------------
           IF (X .GT. XMAX-ONE5) THEN
              A = EXP(X-FORTY)
              B = EXP40
           ELSE
              A = EXP(X)
              B = ONE
           END IF
           RESULT = ((RESULT * A + PBAR * A) / SQRT(X)) * B
           !--------------------------------------------------------------------
           !  Error return for ABS(ARG) .GT. XMAX
           !--------------------------------------------------------------------
        END IF
     END IF
     IF (ARG .LT. ZERO) RESULT = -RESULT
     RETURN
     !----------- Last line of CALCI1 -----------
 
END SUBROUTINE CALCI1

!!$DOUBLE PRECISION FUNCTION BESI1(X)
!!$  !--------------------------------------------------------------------
!!$  !
!!$  ! This long precision subprogram computes approximate values for
!!$  !   modified Bessel functions of the first kind of order one for
!!$  !   arguments ABS(ARG) .LE. XMAX  (see comments heading CALCI1).
!!$  !
!!$  !--------------------------------------------------------------------
!!$  INTEGER JINT
!!$  DOUBLE PRECISION :: X, RESULT
!!$  !--------------------------------------------------------------------
!!$  JINT=1
!!$  CALL CALCI1(X,RESULT,JINT)
!!$  BESI1=RESULT
!!$  RETURN
!!$  !---------- Last line of BESI1 ----------
!!$END FUNCTION BESI1

DOUBLE PRECISION FUNCTION BESEI1(X)
  !--------------------------------------------------------------------
  !
  ! This function program computes approximate values for the
  !   modified Bessel function of the first kind of order one
  !   multiplied by EXP(-ABS(X)), where EXP is the
  !   exponential function, ABS is the absolute value, and X
  !   is any argument.
  !
  !--------------------------------------------------------------------
  INTEGER JINT
  DOUBLE PRECISION X, RESULT
  !--------------------------------------------------------------------
  JINT=2
  CALL CALCI1(X,RESULT,JINT)
  BESEI1=RESULT
  RETURN
  !---------- Last line of BESEI1 ----------
END function BESEI1
